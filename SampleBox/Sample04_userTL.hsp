/*======================================================================
    ユーザタイムライン取得サンプル
------------------------------------------------------------------------
    ユーザタイムラインを取得するサンプルです。
    (このサンプルを実行するには、「Sample01_OAuth.hsp」か
    「Sample02_xAuth.hsp」を実行して「Setting.txt」を作成してくださ
    い。)

  ※このモジュールやサンプルを実行する場合には、Twitter側から
    ConsumerKeyとConsumerSecretを取得する必要があります。詳しくは、モ
    ジュールと同封されている「TsubuyakiSoupドキュメント」を参照して
    ください。
------------------------------------------------------------------------
Author     : takata
LastUpdate : 10/10/16
CreateDate : 10/09/23
======================================================================*/


// モジュールをインクルード
#include "../TsubuyakiSoup.as"

;サンプル用設定ファイル
#include "SampleSetting.hsp"
if CONSUMER_KEY = "" : dialog "「SampleSetting.hsp」でConsumerKeyとConsumerSecretを設定してください。" : end


	// 初期化
	TS_Init "TsubuyakiSoup Sample", CONSUMER_KEY, CONSUMER_SECRET, 20

	// 設定ファイルの有無確認
	xToken = ""
	xSecret = ""
	exist "Setting.txt"
	if strsize != -1 {
		notesel SFA
		noteload "Setting.txt"
		if (Decryption(SFA, "SampleKey")=0) : dialog "ファイルの復号に失敗したため、読み込むことができませんでした。終了します。" : end
		noteget xToken,0
		noteget xSecret,1
		noteunsel
		SetAccessToken xToken, xSecret
	}
	if ( (xToken ="") or (xSecret = "") ) : dialog "「Sample01_OAuth.hsp」か「Sample02_xAuth.hsp」を実行して「Setting.txt」を作成してください。" : end



//---------------------------------
// ユーザタイムラインの取得
//    GetUserTL 命令で取得します。
//    取得するユーザのスクリーン名を第一引数に、件数を第二引数に指定してください。
	GetUserTL "mariko_dayo", 50
	rawText = getBody()
	; エラー判断
	if stat != 200 {
		dialog "タイムラインの取得に失敗しました。終了します。\nステータスコード : "+ stat, 0, "エラー"
		end
	}
//    返ってくる文字列は、UTF-8のXML形式です。XMLパーサ等で解析してください。
//---------------------------------

	newcom oDom,"Microsoft.XMLDOM"
	oDom("async")="False"
	oDom->"loadXML" rawText
	oRoot = oDom("documentElement")
	if varuse(oRoot)=0 : dialog "MSXMLの初期化に失敗しました。" : end
	; テキスト
	comres elm_text
	oDom->"SelectNodes" "/statuses/status/text"
	; スクリーン名
	comres elm_screen_name
	oDom->"SelectNodes" "/statuses/status/user/screen_name"
	; 名前
	comres elm_name
	oDom->"SelectNodes" "/statuses/status/user/name"
	node = elm_name("item", 0)
	Name = node("text")

	ListStr = ""

	repeat elm_text("length")
		; テキスト
		node = elm_text("item", cnt)
		text = node("text")
		; スクリーン名
		node = elm_screen_name("item", cnt)
		screen_name = node("text")
		
		ListStr += screen_name +" : "+ text +"\n"
	loop

	title ""+ Name +"さんのタイムライン"
	mesbox ListStr, ginfo_winx, ginfo_winy, 4

stop
